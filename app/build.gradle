apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'jacoco'

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = files("${buildDir}/jacoco/testDebugUnitTest.exec")
}

android {
    compileSdkVersion 28
    buildToolsVersion '29.0.0'
    defaultConfig {
        applicationId 'by.naxa.soundrecorder'
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 202
        versionName "2.0.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            testCoverageEnabled false
//            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // enable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true
        }
        debug {
            testCoverageEnabled true
            // disable crashlytics
            buildConfigField "boolean", "USE_CRASHLYTICS", "false"
            ext.enableCrashlytics = false
        }
    }
    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
    }
}

afterEvaluate {
    initCrashlyticsPropertiesIfNeeded()
}

def initCrashlyticsPropertiesIfNeeded() {
    def propertiesFile = file('fabric.properties')
    if (!propertiesFile.exists()) {
        // create fabric.properties file using gradle ant task
        def commentMessage = "This is autogenerated crashlytics property from system environment to prevent key to be committed to source control."
        ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
            entry(key: "apiSecret", value: soundrecorderCrashlyticsApiSecret)
            entry(key: "apiKey", value: soundrecorderCrashlyticsApiKey)
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.+'
    implementation 'androidx.cardview:cardview:1.0.+'
    implementation 'androidx.recyclerview:recyclerview:1.0.+'
    implementation 'com.google.android.material:material:1.0.+'
    implementation 'com.googlecode.mp4parser:isoparser:1.1.22'
    implementation 'com.budiyev.android:circular-progress-bar:1.2.0'

    // detect memory leaks
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.1'

//    androidTestImplementation 'org.mockito:mockito-core:2.4.0'
//    androidTestImplementation 'org.powermock:powermock-core:1.7.0RC2'
//    androidTestImplementation 'org.powermock:powermock-module-junit4:1.7.0RC2'
//    androidTestImplementation 'org.powermock:powermock-api-mockito2:1.7.0RC2'
    androidTestImplementation 'androidx.annotation:annotation:1.0.+'
    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.0.0'
    testImplementation 'org.mockito:mockito-core:1.10.19'


    testImplementation 'org.mockito:mockito-core:2.4.0'
    testImplementation 'org.powermock:powermock-core:1.7.0RC2'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.0RC2'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.0RC2'

    androidTestImplementation 'androidx.test:runner:1.1.+'
    androidTestImplementation 'androidx.test:rules:1.1.+'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.+', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'androidx.annotation', module: 'annotation'
    })

    // tracking
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }
}
